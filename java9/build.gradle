plugins {
    id "me.champeau.jmh"
    id "io.github.reyerizo.gradle.jcstress"
}

description = "PerfMark Java9 API"
ext.moduleName = "io.perfmark.java9"
ext.jdkVersion = JavaVersion.VERSION_1_9

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
}

if (!JavaVersion.current().isJava9Compatible()) {
    project.tasks.all {
        task -> task.enabled = false
    }
} else {
    apply plugin: "org.javamodularity.moduleplugin"
}

dependencies {
    implementation project(path: ':perfmark-impl', configuration: 'archives')
    compileOnly libraries.jsr305
    testImplementation project(path: ':perfmark-api', configuration: 'archives')
    jcstressImplementation project(':perfmark-impl')
}

javadoc {
    exclude 'io/perfmark/java9/**'
}

jar {
    exclude 'io/perfmark/java9/Internal*'
}

jmh {
    timeOnIteration = "1s"
    warmup = "1s"
    fork = 1
    warmupIterations = 10

    /*
    profilers = ["perfasm"]

    jvmArgs = [
            "-XX:+UnlockDiagnosticVMOptions",
            "-XX:+LogCompilation",
            "-XX:LogFile=/tmp/blah.txt",
            "-XX:+PrintAssembly",
            "-XX:+PrintInterpreter",
            "-XX:+PrintNMethods",
            "-XX:+PrintNativeNMethods",
            "-XX:+PrintSignatureHandlers",
            "-XX:+PrintAdapterHandlers",
            "-XX:+PrintStubCode",
            "-XX:+PrintCompilation",
            "-XX:+PrintInlining",
            "-XX:+TraceClassLoading",
            "-XX:PrintAssemblyOptions=syntax",
            "-XX:PrintAssemblyOptions=intel"
    ]
    */
}

// ./gradlew --no-daemon clean :perfmark-java9:jcstress
jcstress {
    jcstressDependency 'org.openjdk.jcstress:jcstress-core:0.5'
    // mode "tough"
    deoptRatio "2"
}

compileJcstressJava {
    options.errorprone.excludedPaths = ".*/build/generated/sources/annotationProcessor/.*"
}
