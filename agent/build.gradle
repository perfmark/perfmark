description = "PerfMark Agent"
ext.moduleName = "io.perfmark.agent"
ext.jdkVersion = JavaVersion.VERSION_1_7

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
}

dependencies {
    compileOnly libraries.jsr305,
            libraries.errorprone
    implementation "net.bytebuddy:byte-buddy:1.9.13",
            "org.ow2.asm:asm:7.1",
            "org.ow2.asm:asm-commons:7.1",
            project(":perfmark-api"),
            project(":perfmark-impl")
    testImplementation libraries.truth
    testRuntimeOnly project(":perfmark-java6")

}

import net.ltgt.gradle.errorprone.CheckSeverity

compileTestJava {
    // Broken in JDK12+.  See https://github.com/google/error-prone/issues/1106
    properties.put("errorProne", "false")
    options.errorprone.check("SameNameButDifferent", CheckSeverity.OFF)

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

jar {
    manifest {
        attributes(
                "Premain-Class": "io.perfmark.agent.Main",
                "Main-Class": "io.perfmark.agent.Main\$MySelf",
        )
    }
}

// print sourceSets.main.runtimeClasspath.asPath
