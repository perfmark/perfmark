description = "PerfMark Agent"
ext.moduleName = "io.perfmark.agent"
ext.jdkVersion = JavaVersion.VERSION_1_7

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
}

dependencies {
    compileOnly libraries.jsr305,
            libraries.errorprone
    implementation "org.ow2.asm:asm:9.1",
            "org.ow2.asm:asm-commons:9.1",
            project(":perfmark-api"),
            project(":perfmark-impl")
    testImplementation libraries.truth
    testRuntimeOnly project(":perfmark-java6")

}

// gradle thinks importing this is an error, so load it reflectively.
def checkSeverityOff = Class.forName("net.ltgt.gradle.errorprone.CheckSeverity").getField("OFF").get(null);


compileTestJava {
    // Broken in JDK12+.  See https://github.com/google/error-prone/issues/1106
    properties.put("errorProne", "false")
    options.errorprone.check("SameNameButDifferent", checkSeverityOff)

    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

jar {
    manifest {
        attributes(
                "Premain-Class": "io.perfmark.agent.Main",
                "Main-Class": "io.perfmark.agent.Main\$MySelf",
        )
    }
}

// print sourceSets.main.runtimeClasspath.asPath
