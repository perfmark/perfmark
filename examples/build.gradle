plugins {
    id 'application'
}

description = "PerfMark Examples"
ext.moduleName = "io.perfmark.examples"
ext.jdkVersion = JavaVersion.VERSION_1_8

compileJava {
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
}
configurations {
    perfmarkAgent
}
dependencies {
    implementation project(':perfmark-api'), project(':perfmark-tracewriter')
    runtimeOnly project(':perfmark-java7'), project(':perfmark-java6')

    // Force this to be on the classpath, since it won't come in normally.
    runtimeOnly project(path: ':perfmark-agent')
    perfmarkAgent project(path: ':perfmark-agent', configuration: 'archives')
}

application {
    mainClass = 'io.perfmark.examples.perfetto.WebServer'

    applicationDefaultJvmArgs = [
            "-javaagent:" + configurations.perfmarkAgent.resolve().iterator().next(),
            "-Xlog:class+load=info",
            "-Dio.perfmark.PerfMark.startEnabled=true"
    ]
}

javadoc {
    exclude 'io/perfmark/examples**'
}
